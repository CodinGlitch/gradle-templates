modrinth {
    token = modrinth_token
    projectId = modrinth_id
    versionNumber = "${version}"
    versionName = "[${project.name.toUpperCase()}] ${full_mod_name} ${version}"
    uploadFile = project.name == 'fabric' ? remapJar : jar
    additionalFiles = [sourcesJar, javadocJar]
    gameVersions = supported_versions.split(",").toList()
    loaders = [project.name]
    changelog = file("${project.rootDir}/.changelog").text
    dependencies {
        if (project.name == 'fabric')
            required.project "fabric-api"

        for (final def id in requiredDependencies.split(",") ) {
            required.project id
        }
        for (final def id in optionalDependencies.split(",") ) {
            optional.project id
        }
    }

    debugMode = debug_mode.toBoolean()
}

curseforge {
    apiKey = curseforge_token
    project {
        id = curseforge_id
        changelogType = "markdown"
        changelog = file("${project.rootDir}/.changelog")
        releaseType = 'release'
        relations {
            if (project.name == 'fabric')
                requiredDependency "fabric-api"

            for (final def id in requiredDependencies.split(",") ) {
                requiredDependency id
            }
            for (final def id in optionalDependencies.split(",") ) {
                optionalDependency id
            }
        }

        addGameVersion project.name.capitalize()
        for (final def ver in supported_versions.split(",") ) {
            addGameVersion ver
        }

        mainArtifact(project.name == 'fabric' ? remapJar : jar) {
            displayName = "[${project.name.toUpperCase()}] ${full_mod_name} ${version}"
        }
        addArtifact sourcesJar
        addArtifact javadocJar
    }

    options {
        debug = debug_mode.toBoolean()
    }
}