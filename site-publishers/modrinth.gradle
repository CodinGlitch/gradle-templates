apply from: 'https://raw.githubusercontent.com/CodinGlitch/gradle-templates/main/site-publishers/common.gradle'

var modrinthToken = project.hasProperty('modrinth_token') ? project.property('modrinth_token') : System.getenv("modrinth_token")

if (modrinthToken == null) {
    System.out.println("No token found for modrinth, publishing task will not be loaded properly")
} else {
    modrinth {
        token = modrinthToken
        projectId = modrinth_id
        versionNumber = "${version}"
        versionName = full_name
        versionType = release_type
        uploadFile = tasks.getByName(task_name)
        additionalFiles = [sourcesJar, javadocJar]
        gameVersions = supported_versions.split(",").toList()
        loaders = [loader.toLowerCase()]

        File changelogFile = file("${project.rootDir}/changelog.md")
        if (changelogFile.exists())
            changelog = changelogFile.text

        dependencies {
            if (loader == 'Fabric')
                required.project "fabric-api"

            if (!requiredDependencies.isEmpty()) requiredDependencies.split(",").each { dep -> required.project dep }
            if (!optionalDependencies.isEmpty()) optionalDependencies.split(",").each { dep -> optional.project dep }
        }

        debugMode = debug_mode.toBoolean()
    }
}