apply from: 'https://raw.githubusercontent.com/CodinGlitch/gradle-templates/main/site-publishers/common.gradle'

curseforge {
    apiKey = project.hasProperty('curseforge_token') ? project.property('curseforge_token') : System.getenv("curseforge_token")
    project {
        id = curseforge_id

        changelogType = "markdown"
        File changelogFile = file("${project.rootDir}/changelog.md")
        if (changelogFile.exists())
            changelog = changelogFile.text

        releaseType = release_type
        relations {
            if (loader == 'fabric')
                requiredDependency "fabric-api"

            for (final def id in requiredDependencies.split(",") ) {
                requiredDependency id
            }
            for (final def id in optionalDependencies.split(",") ) {
                optionalDependency id
            }
        }

        addGameVersion loader.capitalize()
        for (final def ver in supported_versions.split(",") ) {
            addGameVersion ver
        }

        mainArtifact(tasks.getByName(loader == 'fabric' ? 'remapJar' : 'jar')) {
            displayName = full_name
        }
        addArtifact sourcesJar
        addArtifact javadocJar
    }

    options {
        debug = debug_mode.toBoolean()
    }
}