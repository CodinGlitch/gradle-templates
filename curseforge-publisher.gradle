curseforge {
    apiKey = curseforge_token
    project {
        id = curseforge_id
        changelogType = "markdown"
        changelog = file("${project.rootDir}/.changelog")
        releaseType = 'release'
        relations {
            if (project.name == 'fabric')
                requiredDependency "fabric-api"

            for (final def id in requiredDependencies.split(",") ) {
                requiredDependency id
            }
            for (final def id in optionalDependencies.split(",") ) {
                optionalDependency id
            }
        }

        addGameVersion project.name.capitalize()
        for (final def ver in supported_versions.split(",") ) {
            addGameVersion ver
        }

        mainArtifact(tasks.getByName(project.name == 'fabric' ? 'remapJar' : 'jar')) {
            displayName = "[${project.name.toUpperCase()}] ${full_mod_name} ${version}"
        }
        addArtifact sourcesJar
        addArtifact javadocJar
    }

    options {
        debug = debug_mode.toBoolean()
    }
}